# SPDX-FileCopyrightText: 2026 Froststrap
#
# SPDX-License-Identifier: MPL-2.0

{
  description = "Flake for Froststrap";

  nixConfig = {
    extra-substituters = [ "https://invra.cachix.org" ];
    extra-trusted-public-keys = [
      "invra.cachix.org-1:5lB/b5n5iQVLCbAYfnH5f5ASzqlVB6uKfINHge7lVvk="
    ];
  };

  inputs = {
    fenix = {
      url = "github:nix-community/fenix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    flake-utils.url = "github:numtide/flake-utils";
    nixpkgs.url = "github:nixos/nixpkgs/26.05";
    treefmt-nix.url = "github:numtide/treefmt-nix";
    self.submodules = true;
  };

  outputs =
    {
      flake-utils,
      nixpkgs,
      treefmt-nix,
      ...
    }@inputs:
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs {
          inherit system;
        };

        froststrap = pkgs.callPackage ./nix/package.nix { };
      in
      {
        devShells =
          let
            inherit (pkgs.callPackage ./nix/devshell-tools.nix { })
              mkComposedShell
              ;

            dotnetFrag = pkgs.callPackage ./nix/dotnetDevShell.nix { };
            extraFrag = pkgs.callPackage ./nix/extra.nix { };
            rustFrag = pkgs.callPackage ./nix/rustDevShell.nix {
              inherit inputs;
            };
          in
          {
            default = mkComposedShell [
              dotnetFrag
              rustFrag
              extraFrag
            ];

            dotnet = mkComposedShell [
              dotnetFrag
            ];

            rust = mkComposedShell [
              rustFrag
            ];
          };

        packages = {
          debug = pkgs.callPackage ./nix/build.nix { };
          inherit froststrap;
          default = froststrap;
        };

        formatter =
          (treefmt-nix.lib.evalModule pkgs (_: {
            projectRootFile = "flake.nix";

            programs = {
              nixfmt.enable = true;
              nixf-diagnose.enable = true;
            };

            settings.formatter = {
              dotnet-format = {
                command = "${pkgs.dotnetCorePackages.sdk_10_0-bin}/bin/dotnet";
                options = [
                  "format"
                ];
                includes = [ "*.csproj" ];
              };
            };
          })).config.build;
      }
    );
}
